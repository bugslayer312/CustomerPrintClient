cmake_minimum_required(VERSION 3.10.0)

project(Generator "NONE")

if (CONFIGURATION STREQUAL "Debug")
  set(DEBUG_POSTFIX "D")
elseif(CONFIGURATION STREQUAL "Release")
  set(DEBUG_POSTFIX "")
else()
  message(SEND_ERROR "Unsupported Configuration \"${CONFIGURATION}\"")
  return()
endif()

file(READ "../src/CustomerPrintClientGui/Version.h" APP_VER)
string(REGEX MATCH "APP_MAJOR_VERSION[ \t]+([0-9]*)" _ ${APP_VER})
set(APP_VER_MAJOR ${CMAKE_MATCH_1})
string(REGEX MATCH "APP_MINOR_VERSION[ \t]+([0-9]*)" _ ${APP_VER})
set(APP_VER_MINOR ${CMAKE_MATCH_1})
string(REGEX MATCH "APP_RELEASE_NUMBER[ \t]+([0-9]*)" _ ${APP_VER})
set(APP_VER_RELEASE ${CMAKE_MATCH_1})
string(REGEX MATCH "APP_SUBRELEASE_NUMBER[ \t]+([0-9]*)" _ ${APP_VER})
set(APP_VER_SUBRELEASE ${CMAKE_MATCH_1})

set(PRODUCT_VERSION "${APP_VER_MAJOR}.${APP_VER_MINOR}.${APP_VER_RELEASE}.${APP_VER_SUBRELEASE}")
set(PRODUCT_DISPLAY_VERSION "${APP_VER_MAJOR}.${APP_VER_MINOR}.${APP_VER_RELEASE}")
set(INSTALLER_NAME "${PRODUCT}${APP_VER_MAJOR}${APP_VER_MINOR}${APP_VER_RELEASE}${DEBUG_POSTFIX}.exe")
message(STATUS "Installer name: ${INSTALLER_NAME}")
message(STATUS "Product: ${PRODUCT}, Configuration: ${CONFIGURATION}")
message(STATUS "Product version: ${PRODUCT_VERSION}")

set(OS_CONDITION_MESSAGE_X86 "This application is only supported on Windows 7 or higher, 32-bit.")
set(OS_CONDITION_X86 "Installed OR (VersionNT >= 601 AND NOT(Msix64))")
set(OS_CONDITION_MESSAGE_X64 "This application is only supported on Windows 7 or higher, 64-bit.")
set(OS_CONDITION_X64 "Installed OR (VersionNT >= 601 AND Msix64)")

set(PLATFORM "x86")
set(PROGRAM_FILES_FOLDER "ProgramFilesFolder")
set(OS_CONDITION_MESSAGE ${OS_CONDITION_MESSAGE_X86})
set(OS_CONDITION ${OS_CONDITION_X86})
set(LIB_CRYPTO_COMMENT_BEGIN "<!--")
set(LIB_CRYPTO_COMMENT_END "-->")
if (LIB_CRYPTO_X86)
  set(LIB_CRYPTO ${LIB_CRYPTO_X86})
  set(LIB_CRYPTO_COMMENT_BEGIN "")
  set(LIB_CRYPTO_COMMENT_END "")
endif()
message(STATUS "Generating file ${PRODUCT}32.wxs")
configure_file("Product.wxs.template" "${PRODUCT}32.wxs" @ONLY)

set(PLATFORM "x64")
set(PROGRAM_FILES_FOLDER "ProgramFiles64Folder")
set(OS_CONDITION_MESSAGE ${OS_CONDITION_MESSAGE_X64})
set(OS_CONDITION ${OS_CONDITION_X64})
set(LIB_CRYPTO_COMMENT_BEGIN "<!--")
set(LIB_CRYPTO_COMMENT_END "-->")
if (LIB_CRYPTO_X64)
  set(LIB_CRYPTO ${LIB_CRYPTO_X64})
  set(LIB_CRYPTO_COMMENT_BEGIN "")
  set(LIB_CRYPTO_COMMENT_END "")
endif()
message(STATUS "Generating file ${PRODUCT}64.wxs")
configure_file("Product.wxs.template" "${PRODUCT}64.wxs" @ONLY)

message(STATUS "Generating file ${PRODUCT}Bundle.wxs")
configure_file("Bundle.wxs.template" "${PRODUCT}Bundle.wxs" @ONLY)

message(STATUS "Generating file ${PRODUCT}.outputfiles")
file(WRITE "${CMAKE_CURRENT_BINARY_DIR}/${PRODUCT}.outputfiles" "${INSTALLER_NAME}")